source: airports is table('bigquery:malloy-data.faa.airports') {
  primary_key: code

  rename: 
    `State` is state
    `FAA Region Code` is faa_region
    `Facility Type` is fac_type

  measure: `Airport Count` is count()

  dimension: name is concat(code, ' - ', full_name)
  dimension: `FAA Region Name` is `FAA Region Code` ?
    pick 'Southwest' when 'ASW'
    pick 'Northwest Mountain' when 'ANM'
    pick 'Eastern' when 'AEA'
    pick 'Southern' when 'ASO'
    pick 'Great Lakes' when 'AGL'
    pick 'Central' when 'ACE'
    pick 'New England' when 'ANE'
    pick 'Western Pacific' when 'AWP'
    pick 'Alaska' when 'AAL'

  query: `Regions` is {
    group_by: `FAA Region Code`
    group_by: `FAA Region Name`
    aggregate: `Airport Count`
  }
  query: `States` is {
    group_by: `State`
    aggregate: `Airport Count`
  }

  # bar_chart
  query: `Facility Types` is {
    group_by: `Facility Type`
    aggregate:
      `Airport Count`
      `Average Elevation` is elevation.avg()
  }

  query: `Major Airports` is {
    where: major = 'Y'
    group_by: `State`
    # list
    nest: Airports is { group_by: code }
  }

  # dashboard
  query: `Region Dashboard`is `Regions` + {
    where: `FAA Region Code` != null
    nest:
      # shape_map
      `States`
      # bar_chart
      `Facility Types`
      `Major Airports`
  }
}
